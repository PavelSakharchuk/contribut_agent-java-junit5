/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java'
}

project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (project.hasProperty('reportDisabledTests')) {
    systemProperty('reportDisabledTests', reportDisabledTests)
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url "https://jitpack.io" }
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:5.0.0-BETA-9'
        compile 'com.epam.reportportal:commons-model:5.0.0'
    } else {
        compile 'com.github.reportportal:client-java:51aef16'
        compile 'com.github.reportportal:commons-model:5.0.0'
    }

    compileOnly("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    compile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    compile("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")

    testImplementation 'org.junit.platform:junit-platform-runner:1.6.0'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.6.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    exclude("com/epam/reportportal/junit5/features/**")
    maxParallelForks(1);
}

wrapper {
    gradleVersion = '5.4.1'
}

afterReleaseBuild.dependsOn bintrayUpload
